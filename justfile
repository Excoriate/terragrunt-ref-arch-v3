# ğŸ—ï¸ Terragrunt Reference Architecture - Justfile
# This Justfile provides a streamlined interface for managing Terragrunt-based infrastructure
# Designed to simplify complex infrastructure workflows and provide consistent, reproducible deployments

# ğŸ“ Path configurations
# Centralize path management to ensure consistent directory references across recipes
TERRAGRUNT_DIR := "./infra/terragrunt"
TERRAFORM_MODULES_DIR := "./infra/terraform/modules"

# ğŸ“ Default values for Terragrunt environment, stack, and unit
# TODO: Change it accordingly to the environment, stack, and unit you are working on
# ğŸ“ Terragrunt default(s)
TG_DEFAULT_STACK := "non-distributable"
TG_DEFAULT_UNIT := "random-string-generator"

# ğŸš Shell configuration
# Use bash with strict error handling to prevent silent failures
# -u: Treat unset variables as an error
# -e: Exit immediately if a command exits with a non-zero status
set shell := ["bash", "-uce"]
set dotenv-load

# Avoid reporting traces to Dagger
# TODO: Uncomment if traces aren't needed.
# export NOTHANKS := "1"

# ğŸ“‹ Default recipe: List all available commands
# Provides a quick overview of available infrastructure management commands
default:
    @just --list

# ğŸŒ¿ Start Nix Development Shell
dev:
    @echo "ğŸŒ¿ Starting Nix Development Shell for Terragrunt Reference Architecture ğŸš€"
    @nix develop . --impure --extra-experimental-features nix-command --extra-experimental-features flakes

# ğŸ—‘ï¸ Clean macOS system files
# Removes .DS_Store files that can cause unnecessary version control noise
# Helps maintain a clean repository across different operating systems
clean-ds:
    @echo "ğŸ§¹ Cleaning .DS_Store files"
    @find . -name '.DS_Store' -type f -delete

# ğŸ”§ Install pre-commit hooks in local environment for code consistency
hooks-install:
    @echo "ğŸ§° Installing pre-commit hooks locally..."
    @./scripts/hooks/pre-commit-init.sh init

# ğŸ•µï¸ Run pre-commit hooks across all files in local environment
hooks-run:
    @echo "ğŸ” Running pre-commit hooks from .pre-commit-config.yaml..."
    @./scripts/hooks/pre-commit-init.sh run

# ğŸ› ï¸ Allow direnv to run
# Ensures that direnv is allowed to run in the current directory
# Useful for managing environment variables and configurations
allow-direnv:
    @echo "ğŸ”’ Allow direnv to run..."
    @direnv allow

# ğŸ”„ Reload direnv environment
# Manually reload the direnv environment when needed
reload-env:
    @echo "ğŸ”„ Manually reloading direnv environment..."
    @direnv reload

# ğŸ§¹ Clean direnv cache
# Removes the direnv cache to force a fresh environment build
# Useful when experiencing issues with the development environment
clean-direnv:
    @echo "ğŸ§¹ Cleaning direnv cache..."
    @rm -rf .direnv
    @direnv allow
    @echo "âœ… direnv cache cleaned. Environment will rebuild on next shell activation."

# ğŸ” Run Terraform command for a specific module
[working-directory:'infra/terraform/modules']
tf-run module='random-string-generator' cmd='init' args='':
    @echo "ğŸ” Preparing to run Terraform command..."
    @echo "ğŸ“‚ Module Path: {{module}}"
    @echo "âš™ï¸ Command: {{cmd}}"
    @echo "ğŸ“‹ Arguments: {{args}}"
    @cd {{module}} && terraform {{cmd}} {{args}}
    @echo "âœ… Terraform {{cmd}} executed successfully for module: {{module}}"

# ğŸŒ¿ Format all Terraform files across modules, examples, and tests directories
tf-format-all:
    @echo "ğŸŒ¿ Formatting all Terraform files across the repository..."
    @echo "ğŸ“‚ Scanning directories: {{TERRAFORM_MODULES_DIR}}/"

    @echo "\nğŸ” Formatting files in modules/"
    @pushd {{TERRAFORM_MODULES_DIR}} > /dev/null && \
    find . -type f \( -name "*.tf" -o -name "*.tfvars" \) | sort | while read -r file; do \
        echo "   ğŸ“„ Processing: $file"; \
    done && \
    terraform fmt -recursive && \
    popd > /dev/null

    @echo "\nâœ… All Terraform files have been formatted!"

# ğŸ§¹ Clean Terraform cache for all modules
[working-directory:'infra/terraform/modules']
tf-clean-all:
    @echo "ğŸ§¹ Cleaning Terraform cache for all modules"
    @if [ -n "$(find . -maxdepth 4 -type d -name ".terraform" 2>/dev/null)" ]; then \
        echo "ğŸ” Found .terraform directories to clean"; \
        find . -maxdepth 4 -type d -name ".terraform" -exec rm -rf {} +; \
        echo "âœ… Removed .terraform directories"; \
    else \
        echo "â„¹ï¸ No .terraform directories found"; \
    fi
    @if [ -n "$(find . -maxdepth 4 -type f -name ".terraform.lock.hcl" 2>/dev/null)" ]; then \
        echo "ğŸ” Found .terraform.lock.hcl files to clean"; \
        find . -maxdepth 4 -type f -name ".terraform.lock.hcl" -exec rm -rf {} +; \
        echo "âœ… Removed .terraform.lock.hcl files"; \
    else \
        echo "â„¹ï¸ No .terraform.lock.hcl files found"; \
    fi
    @echo "ğŸ§¹ Cleaning completed"

# ğŸ§¹ Terragrunt and Terraform cache cleanup
[working-directory:'infra/terragrunt']
tg-clean-all:
    @echo "ğŸ§¹ Cleaning Terragrunt cache for all environments and .terraform directories"
    @find . -maxdepth 4 -type d \( -name ".terragrunt-cache" -o -name ".terraform" \) -exec rm -rf {} +
    @find . -maxdepth 4 -type f -name ".terraform.lock.hcl" -exec rm -rf {} +
    @find . -maxdepth 4 -type f -name ".terraform.lock.hcl" -exec rm -rf {} +

# ğŸ§¹ Terragrunt and Terraform cache cleanup for a specific path
[working-directory:'infra/terragrunt']
tg-clean tgpath:
    @echo "ğŸ§¹ Cleaning Terragrunt cache for specific path: {{tgpath}}"
    @if [ -d {{tgpath}} ]; then \
        cd {{tgpath}} && \
        find . -maxdepth 4 -type d \( -name ".terragrunt-cache" -o -name ".terraform" \) -exec rm -rf {} + && \
        find . -maxdepth 4 -type f -name ".terraform.lock.hcl" -exec rm -rf {} +; \
    else \
        echo "âŒ Directory {{tgpath}} does not exist."; \
    fi

# ğŸ§¹ Terragrunt format, run hclfmt on all Terragrunt files
# Example: `just tg-format check=true diff=true exclude=".terragrunt-cache,modules"`
tg-format check="false" diff="false" exclude="":
    @echo "ğŸ” Running Terragrunt HCL formatting via utility script"
    @./scripts/justfile-utils.sh terragrunt_format "{{TERRAGRUNT_DIR}}" "{{check}}" "{{diff}}" "{{exclude}}"

# âœ… Terragrunt validate, run hclvalidate on all Terragrunt files
# Example: `just tg-hclvalidate`
tg-hclvalidate:
    @echo "âœ… Running Terragrunt HCL validation via utility script"
    @./scripts/justfile-utils.sh terragrunt_hclvalidate "{{TERRAGRUNT_DIR}}"

# ğŸš€ Run Terragrunt CI checks (hclvalidate and format)
tg-ci: (tg-hclvalidate) (tg-format)

# ğŸš€ Run Terragrunt on a specific infrastructure unit
# Example: `just tg-run env=dev stack=non-distributable unit=random-string-generator cmd=init`
[working-directory:'infra/terragrunt']
tg-run env stack unit cmd="init":
    @cd {{env}}/{{stack}}/{{unit}} && \
    if [ "{{cmd}}" = "apply" ] || [ "{{cmd}}" = "destroy" ]; then \
        echo "ğŸ”’ Running Terragrunt {{cmd}} with auto-approve flag" && \
        terragrunt {{cmd}} --auto-approve; \
    else \
        echo "ğŸ” Running Terragrunt {{cmd}}" && \
        terragrunt {{cmd}}; \
    fi

# ğŸš€ Run Terragrunt on all infrastructure units in a stack
# Example: `just tg-run-all env=dev stack=non-distributable unit=random-string-generator cmd=init`
[working-directory:'infra/terragrunt']
tg-run-all env stack cmd="init":
    @cd {{env}}/{{stack}} && terragrunt run-all {{cmd}} $(if [ "{{cmd}}" = "apply" ] || [ "{{cmd}}" = "destroy" ]; then echo "--auto-approve"; fi)


# ğŸ”¨ Build the Dagger pipeline
[working-directory:'pipeline/infra']
pipeline-infra-build:
    @echo "ğŸ”¨ Initializing Dagger development environment"
    @dagger develop
    @echo "ğŸ“‹ Building Dagger pipeline"
    @dagger functions

# ğŸ”¨ Help for Dagger job
[working-directory:'pipeline/infra']
pipeline-job-help fn: (pipeline-infra-build)
    @echo "ğŸ”¨ Help for Dagger job: {{fn}}"
    @dagger call {{fn}} --help

# ğŸ”¨ Open an interactive development shell for the Infra pipeline
[working-directory:'pipeline/infra']
pipeline-infra-shell args="": (pipeline-infra-build)
    @echo "ğŸš€ Launching interactive terminal"
    @dagger call open-terminal {{args}}

# ğŸ”¨ Validate Terraform modules for best practices and security
[working-directory:'pipeline/infra']
pipeline-infra-tf-modules-static-check args="": (pipeline-infra-build)
    @echo " Analyzing Terraform modules for security and best practices"
    @echo "âš¡ Running static analysis checks"
    @dagger call job-tf-modules-static-check {{args}}
    @echo "âœ… Static analysis completed successfully"

# ğŸ”¨ Test Terraform modules against multiple provider versions
[working-directory:'pipeline/infra']
pipeline-infra-tf-modules-versions args="": (pipeline-infra-build)
    @echo " Testing module compatibility across provider versions"
    @echo "âš¡ Running version compatibility checks"
    @dagger call job-tf-modules-compatibility-check {{args}}
    @echo "âœ… Version compatibility testing completed"

# ğŸ”¨ Run comprehensive CI checks for Terraform modules
pipeline-infra-tf-ci args="": (pipeline-infra-tf-modules-static-check) (pipeline-infra-tf-modules-versions)

# ğŸ”¨ Run a Terragrunt job with custom arguments
[working-directory:'pipeline/infra']
pipeline-infra-tg-exec args="": (pipeline-infra-build)
    @echo "ğŸ”¨ Running Terragrunt single command through Dagger"
    @dagger call job-tg-exec {{args}}

# ğŸ”¨ Run a Terragrunt job with custom arguments
[working-directory:'pipeline/infra']
pipeline-infra-tg-ci-static env="global" stack="dni": (pipeline-infra-build)
    @echo "ğŸ”„ Running Terragrunt CI checks through Dagger"
    @echo "ğŸŒ Environment: {{env}} | ğŸ“š Stack: {{stack}}"
    @dagger call job-citg-stack-static-analysis \
        --aws-region eu-west-1 \
        --aws-access-key-id env:AWS_ACCESS_KEY_ID \
        --aws-secret-access-key env:AWS_SECRET_ACCESS_KEY \
        --load-dot-env \
        --no-cache \
        --environment "{{env}}" \
        --stack "{{stack}}"

    @echo "âœ… Terragrunt CI checks completed successfully on environment: {{env}} | ğŸ“š Stack: {{stack}}"

# ğŸ”¨ Run a Terragrunt job on stacks, with custom arguments
[working-directory:'pipeline/infra']
pipeline-infra-tg-stack env="dev" stack="non-distributable" tg-cmd="validate" tg-cmd-args="--terragrunt-ignore-external-dependencies": (pipeline-infra-build)
    @echo "ğŸ”„ Running Terragrunt stack through Dagger"
    @echo "ğŸŒ Environment: {{env}} | ğŸ“š Stack: {{stack}}"
    @echo "ğŸ”¨ Terragrunt command: {{tg-cmd}}"
    @echo "ğŸ”¨ Terragrunt command arguments: {{tg-cmd-args}}"
    @dagger call job-tg-stack \
        --aws-region eu-west-1 \
        --aws-access-key-id env:AWS_ACCESS_KEY_ID \
        --aws-secret-access-key env:AWS_SECRET_ACCESS_KEY \
        --load-dot-env \
        --no-cache \
        --environment "{{env}}" \
        --stack "{{stack}}" \
        --git-ssh $SSH_AUTH_SOCK \
        --tg-cmd "{{tg-cmd}}" \
        --tg-cmd-args "{{tg-cmd-args}}"

    @echo "âœ… Terragrunt stack completed successfully on environment: {{env}} | ğŸ“š Stack: {{stack}}"
